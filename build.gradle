plugins {
    id 'java-library'
    id 'maven-publish'
    id 'signing'
}

repositories {
    mavenCentral()
}

dependencies {
    implementation 'com.fazecast:jSerialComm:2.10.4'

    testImplementation platform('org.junit:junit-bom:5.9.1')
    testImplementation 'org.junit.jupiter:junit-jupiter'
}

java {
    withJavadocJar()
    withSourcesJar()
}

test {
    useJUnitPlatform()
}

boolean publish = project.hasProperty('ossrh_user')

if (!publish) {
    println('=== Publish skipped because no credential is provided. ===')
}

publishing {
    if (publish) {
        publications {
            pub(MavenPublication) {
                from components.java

                groupId = 'io.github.llschall'
                artifactId = 'jtrafficlight'
                version = '1.0.0-SNAPSHOT'

                pom {
                    signing {
                        sign publishing.publications.pub
                        sign configurations.archives
                    }
                    name = 'jtrafficlight'
                    description = 'Plug a traffic light to an Arduino board and control it from your Java application.'
                    url = 'https://github.com/llschall/jtrafficlight'
                    licenses {
                        license {
                            name = 'The Apache License, Version 2.0'
                            url = 'http://www.apache.org/licenses/LICENSE-2.0.txt'
                        }
                    }
                    developers {
                        developer {
                            id = 'llschall'
                            name = 'Laurent Schall'
                            email = 'llschall@gmx.com'
                        }
                    }
                    scm {
                        url = 'https://github.com/llschall/jtrafficlight.git'
                    }
                }
            }
        }
        repositories {
            maven {
                credentials {
                    username "$ossrh_user"
                    password "$ossrh_pwd"
                }
                url "https://s01.oss.sonatype.org/content/repositories/snapshots"
            }
        }
        signing {
            sign publishing.publications.pub
        }
    }
}

javadoc {
    if (JavaVersion.current().isJava9Compatible()) {
        options.addBooleanOption('html5', true)
    }
}

